#! /usr/bin/env zsh

set -euo pipefail

# exec > Makefile

declare -A resources

decl_resources() {
	# Shell
	resources[\$(HOME)/.zshrc]='zsh/zshrc.zsh'

	# Tmux
	resources[\$(HOME)/.tmux.conf]='tmux/oh-my-tmux/.tmux.conf'
	resources[\$(HOME)/.tmux.conf.local]='tmux/local.tmux.conf'
	resources[\$(HOME)/.tmux/plugins/tpm]='tmux/tpm'

	# VIM
	resources[\$(HOME)/.vimrc]='neovim/init.vim'
	resources[\$(HOME)/.config/nvim/after]='neovim/after'
	resources[\$(HOME)/.vim/after]='neovim/after'
	resources[\$(HOME)/.vim/autoload/plug.vim]='neovim/plug.vim'
	resources[\$(HOME)/.config/nvim/init.lua]='neovim/init.lua'

	## Old .vim settings
	#resources[\$(HOME)/.config/nvim/init.vim]='neovim/init.vim'
	#resources[\$(HOME)/.local/share/nvim/site/autoload/plug.vim]='neovim/plug.vim'

	# asdf
	resources[\$(HOME)/.asdf]='asdf'

	# starship
	resources[\$(HOME)/.config/starship.toml]='starship/starship.toml'

	# i3
	resources[\$(HOME)/.i3/config]='i3/i3config'
	resources[\$(HOME)/.config/i3/config]='i3/i3config'
	resources[\$(HOME)/.config/i3status-rust/config.toml]='i3/i3status-rs.conf.toml'

	# Alacritty
	resources[\$(HOME)/.config/alacritty/alacritty.yml]='alacritty/alacritty.yml'
}


generate_makefile() {
	printf "# Auto genrated from ./configure\n\n"

	printf '
export PATH := $(DOTFILES_HOME)/_scripts:$(PATH)

define link
@safe-link.sh $< $@
endef\n\n'

	printf "
.PHONY: all
all:\n\n"

	declare -A rules
	declare -A deps

	for target src in ${(kv)resources}; do 
		pkg=${src%%/*}

		deps[$pkg]+=" $target"

		rules[$pkg]+="
.PHONY: $target
$target: \$(DOTFILES_HOME)/$src
	\$(link)
"
	done

	for pkg targets in ${(kv)rules}; do
		printf "
.PHONY: %s
%s:%s
%s
" "$pkg" "$pkg" "${deps[$pkg]}" "$targets"
	done

	printf "\nall: %s\n" "$(echo "${(k)deps}")"
}


main() {
	local target_mkfile=Makefile

	for arg; do
		case $arg in
			--print) target_mkfile=/dev/stdout;;
			*) echo "Unknown argument: $arg" >&2; exit 1 ;;
		esac
	done

	decl_resources
	generate_makefile > "$target_mkfile"
}


main "$@"
